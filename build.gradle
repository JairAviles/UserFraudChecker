plugins {
	id 'java'
	id 'jacoco'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'co.rooam'
version = '1.0.0'

java {
	sourceCompatibility = '21'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.amazonaws.serverless:aws-serverless-java-container-springboot3:2.0.0'
	implementation 'net.datafaker:datafaker:2.1.0'
	implementation 'org.slf4j:slf4j-api:2.0.11'
	implementation 'com.google.code.findbugs:jsr305:3.0.2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
	testImplementation 'org.mockito:mockito-core:5.10.0'
	testImplementation 'org.mockito:mockito-junit-jupiter:5.10.0'
}

// Code coverage report
jacocoTestReport {
	reports {
		xml.required = false
		csv.required = false
	}
}

// Task for building the zip file for upload
tasks.register('buildZip', Zip) {
	from compileJava
	from processResources
	into('lib') {
		from(configurations.compileClasspath) {
			exclude 'tomcat-embed-*'
		}
	}
}

build.dependsOn buildZip

test {
	useJUnitPlatform()
}